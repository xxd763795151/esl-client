plugins {
    id "java"
    id "maven-publish"
    id "signing"
}

group = 'io.github.xxd763795151'
version = '1.0.0'

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
        from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        title "FreeSWITCH ESL Client"
        failOnError false
        noTimestamp true
        addStringOption '-Xdoclint:none'
    }
}

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'central'
        url = 'https://repo1.maven.org/maven2/'
    }
}

dependencies {
    implementation 'io.netty:netty-all:4.1.65.Final'
    implementation 'com.google.guava:guava:23.4-jre'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'junit:junit:4.8.1'
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java

            artifact sourcesJar
            artifact javadocJar
            
            pom {
                name = 'FreeSWITCH ESL Client'
                description = 'FreeSWITCH Event Socket Library Client for Java'
                url = 'https://github.com/xxd763795151/esl-client'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'xxd763795151'
                        name = 'Xiaodong Xu'
                        email = '763795151@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/xxd763795151/esl-client.git'
                    developerConnection = 'scm:git:ssh://github.com/xxd763795151/esl-client.git'
                    url = 'https://github.com/xxd763795151/esl-client'
                }
            }
        }
    }

    tasks.register('generateChecksums') {
        dependsOn publishToMavenLocal
        doLast {
            def localRepoDir = new File(System.getProperty('user.home'), '.m2/repository/io/github/xxd763795151/esl-client/1.0.0')
            if (localRepoDir.exists() && localRepoDir.isDirectory()) {
                localRepoDir.eachFile { file ->
                    if (!file.name.endsWith('.md5') && !file.name.endsWith('.sha1') && !file.isDirectory()) {
                        def md5File = new File(file.parent, file.name + '.md5')
                        def md5Digest = java.security.MessageDigest.getInstance('MD5')
                        file.eachByte(8192) { buffer, length ->
                            md5Digest.update(buffer, 0, length)
                        }
                        def md5Hash = md5Digest.digest().collect { String.format('%02x', it) }.join('')
                        md5File.text = md5Hash

                        def sha1File = new File(file.parent, file.name + '.sha1')
                        def sha1Digest = java.security.MessageDigest.getInstance('SHA-1')
                        file.eachByte(8192) { buffer, length ->
                            sha1Digest.update(buffer, 0, length)
                        }
                        def sha1Hash = sha1Digest.digest().collect { String.format('%02x', it) }.join('')
                        sha1File.text = sha1Hash

                        println "Generated checksums for ${file.name}"
                    }
                }
            } else {
                println "Local Maven repository directory not found: ${localRepoDir}"
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'central'
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//            url = version.endsWith('SNAPSHOT')
//                ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
//                : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username System.getenv('MAVEN_USERNAME') ?: project.findProperty('mavenUsername')
                password System.getenv('MAVEN_PASSWORD') ?: project.findProperty('mavenPassword')
                println "Username: $username"
            }
        }
    }
}

if (project.hasProperty('signing.enabled') && project.property('signing.enabled') == 'true') {
    signing {
        useGpgCmd()
        sign publishing.publications.myLibrary

    }
}

